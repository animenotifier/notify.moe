component InputText(id string, value string, label string, placeholder string)
	.widget-section
		label(for=id)= label + ":"
		input.widget-ui-element.action(id=id, data-field=id, type="text", value=value, placeholder=placeholder, title=placeholder, data-action="save", data-trigger="change")

component InputBool(id string, value bool, label string, title string)
	.widget-section
		label(for=id)= label + ":"
		if value
			button.action(id=id, data-action="disable", data-trigger="click", data-field=id, title=title)
				Icon("toggle-on")
				span ON
		else
			button.action(id=id, data-action="enable", data-trigger="click", data-field=id, title=title)
				Icon("toggle-off")
				span OFF
		//- input.widget-ui-element.action(id=id, data-field=id, type="checkbox", value=value, checked=value, data-action="save", data-trigger="change")

component InputTextArea(id string, value string, label string, placeholder string)
	.widget-section
		label(for=id)= label + ":"
		textarea.widget-ui-element.action(id=id, data-field=id, placeholder=placeholder, title=placeholder, data-action="save", data-trigger="change")= value

component InputNumber(id string, value float64, label string, placeholder string, min string, max string, step string)
	.widget-section
		label(for=id)= label + ":"
		input.widget-ui-element.action(id=id, data-field=id, type="number", value=value, min=min, max=max, step=step, placeholder=placeholder, title=placeholder, data-action="save", data-trigger="change")

component InputNumberWithButtons(id string, value float64, label string, placeholder string, min string, max string, step string)
	.widget-section
		label(for=id)= label + ":"
		.number-input-container
			input.widget-ui-element.action(id=id, data-field=id, type="number", value=value, min=min, max=max, step=step, placeholder=placeholder, title=placeholder, data-action="save", data-trigger="change")
			button.action.tip(data-action="addNumber", data-trigger="click", data-id=id, data-add="1", aria-label="Increase by 1")
				RawIcon("plus")
			button.action.tip(data-action="addNumber", data-trigger="click", data-id=id, data-add="-1", aria-label="Decrease by 1")
				RawIcon("minus")

component InputSelection(id string, value string, label string, placeholder string, options []*arn.Option)
	.widget-section
		label(for=id)= label + ":"
		select.widget-ui-element.action(id=id, data-field=id, value=value, title=placeholder, data-action="save", data-trigger="change")
			each option in options
				option(value=option.Value)= option.Label

component InputFileUpload(id string, label string, uploadType string, endpoint string)
	.widget-section
		label(for=id)= label + ":"
		button.action(id=id, data-action="selectFile", data-trigger="click", data-endpoint=endpoint, data-type=uploadType)
			Icon("upload")
			span Select file

component InputTags(id string, value []string, label string, tooltip string)
	.widget-section
		label(for=id)= label + ":"
		.tags(id=id)
			for index, tag := range value
				.tag.tag-edit.action(contenteditable="true", data-action="save", data-trigger="focusout", data-field=id + "[" + strconv.Itoa(index) + "]")= tag
				button.tag-remove.action(data-action="arrayRemove", data-trigger="click", data-field=id, data-index=index)
					RawIcon("trash")
			
			button.tag-add.action(data-action="arrayAppend", data-trigger="click", data-field=id, title="Add more")
				RawIcon("plus")
		
		p!= tooltip
			